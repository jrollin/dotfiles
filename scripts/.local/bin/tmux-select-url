#!/usr/bin/env zsh

## source: https://dev.to/tomoviktor/best-way-to-open-urls-in-your-terminal-via-tmux-595b
# https://github.com/11Firefox11/.dotfiles/blob/84f80130b9f5c5b21ebfceb4cc9b3b7b232b6e51/bin/.local/scripts/tmux-select-url

name="select-url"
message_time_period=1500
buffer_file_name="tmuxsave"
menu_max_items=5

# OS-aware URL opener
if [[ "$(uname)" == "Darwin" ]]; then
	url_open="open"
else
	url_open="xdg-open"
fi

# --- actual script starts here ---

if [ -z "$TMUX" ]; then
	echo "This script must be ran with TMUX variable context."
	exit 1
fi

is_full=false
if [[ $1 == "full" ]]; then
	tmux capture-pane -S "-"
else
	tmux capture-pane
fi
tmux save-buffer "${TMPDIR:-/tmp}/${buffer_file_name}"
tmux delete-buffer

urls=$(grep -Eoi "(http|https)://[a-zA-Z0-9+./?=_%:-]+" "${TMPDIR:-/tmp}/${buffer_file_name}" | uniq)
if [[ -z $urls ]]; then
	tmux display-message -d "$message_time_period" "#[fg=red bold]$name: No URL found#[bg=red bold]"
	exit 0
fi

url_count=$(echo "$urls" | wc -l)
if [ $url_count -eq 1 ]; then
	$url_open $urls > /dev/null 2>&1 && tmux display-message -d $message_time_period "#[bold]$name: Opened $urls#[bold]"
elif [ $url_count -gt $menu_max_items ]; then
	tmux neww -n "url-select" "printf '%s\n' '$urls' | fzf --exit-0 --print-query | xargs -I {} sh -c '$url_open {} > /dev/null 2>&1 && tmux display-message \"#[bold]$name: Opened {}#[bold]\"'"
else
	tmux_menu="tmux display-menu -t 1 -T 'url-select'"
	index=1
	for url in ${(f)urls}; do
		clean_url=${url#*//}

		tmux_menu="$tmux_menu '$clean_url' $index 'run-shell \"$url_open $url > /dev/null 2>&1; tmux display-message '\''#[bold]$name: Opened $url#[bold]'\''\"'"

		((index++))
		if [ $index -gt 9 ]; then
			break
		fi
	done

	eval $tmux_menu
fi

# vim: ts=2 sts=2 sw=2 et
